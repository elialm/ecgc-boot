include "boot.inc"
include "ecgc_cart.inc"
include "hardware.inc"
include "macros.inc"

; DRAM_TEST_UPPER_BANK = $07FF
DRAM_TEST_UPPER_BANK = $0010
DRAM_TEST_RNG_BYTES_COUNT = $4000

section "DRAM routines", rom0

dram_start_test::
    ; Initialise loop counter
    ldh [bank_counter], a
    ldh [bank_counter+1], a

    ; Initialise rand routine
    ld a, [dram_test_rand_seed]
    call rand_init

    ; Halt till next VBlank, then disable LCD
    halt
    nop
    ld hl, rLCDC
    res 7, [hl]

    ; Scroll history and empty line
    call scroll_history
    LOAD_CELL_SCRN0_HL 1, 1
    ld d, 9
    xor a, a
    call memset
    call scroll_history

    ; Print read progress line
    LOAD_CELL_SCRN0_HL 1, 1
    ld a, "R"
    ld [hl+], a
    ld a, "B"
    ld [hl+], a
    inc hl
    ld a, "0"
    ld d, 4
    call memset

    ; Scroll history to lower read progress line
    call scroll_history

    ; Print write progress line
    LOAD_CELL_SCRN0_HL 1, 1
    ld a, "W"
    ld [hl+], a
    ld a, "B"
    ld [hl+], a
    inc hl
    ld a, "0"
    ld d, 4
    call memset

    ; Enable LCD
    ld hl, rLCDC
    set 7, [hl]

.bank_loop:
    ; Set DRAM bank
    ld a, [bank_counter]
    ld [CART_DRAM_SEL0], a
    ld a, [bank_counter+1]
    and a, %00000111
    ld [CART_DRAM_SEL1], a

    ; Update screen write status
    LCD_DISABLE
    LOAD_CELL_SCRN0_HL 4, 1

    ; Print bank_counter high byte
    ld a, [bank_counter+1]
    ld d, a
    call d2hex
    ld [hl], b
    inc hl
    ld [hl], c
    inc hl

    ; Print bank_counter low byte
    ld a, [bank_counter]
    ld d, a
    call d2hex
    ld [hl], b
    inc hl
    ld [hl], c

    LCD_ENABLE

    ; Set DRAM pointer
    ld l, $00
    ld h, $40

    ; Fill bank with random data
    ld d, high(DRAM_TEST_RNG_BYTES_COUNT)
    ld e, low(DRAM_TEST_RNG_BYTES_COUNT)
.rng_loop:
    call rand
    ld [hl+], a
    dec de
    xor a, a
    cp a, d
    jr nz, .rng_loop
    cp a, e
    jr nz, .rng_loop

    ; Increment bank counter and check if bank loop still needs to run
    ld hl, bank_counter
    inc [hl]    
    jr nz, .skip_upper_inc
    inc hl
    inc [hl]
    dec hl
.skip_upper_inc:
    ; incremented bank counter is stored to ram
    inc hl
    ld a, high(DRAM_TEST_UPPER_BANK)
    cp a, [hl]
    jr nz, .bank_loop
    dec hl
    ld a, low(DRAM_TEST_UPPER_BANK)
    cp a, [hl]
    jr nz, .bank_loop

    ret

; Random number seed
dram_test_rand_seed:
    db $6E

section "DRAM routines variables", hram

bank_counter:
    ds 2